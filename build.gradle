plugins {

    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.beryx.runtime' version '1.12.7'

}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.xmlgraphics:batik-all:1.16'
}

javafx {

    version '19'
    modules = [ 'javafx.graphics', 'javafx.controls', 'javafx.swing' ]

}

application {

    mainClass = 'gui.Chess'
    applicationName = 'Chess'
    
}

jar {
    manifest {
        attributes 'Implementation-Version': '1.0.2'
    }
}

task genJavadocs(type: Javadoc) {
    options.showFromPrivate()
    source = sourceSets.main.allJava
    classpath = project.sourceSets.main.runtimeClasspath
}

runtime {

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        noConsole = true
    }

    jpackage {

        mainClass = 'gui.Chess'
        applicationName = 'Chess'
        imageName = 'Chess'
        appVersion = '1.0.2'

        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'

        imageOptions += ['--icon', "src/main/resources/img/icon.$imgType"]

        installerOptions += ['--resource-dir', "src/main/resources", '--vendor', 'Stephen Santilli']

        if(currentOs.windows) {
            installerOptions += ['--type', 'exe', '--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'chess','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'Chess']
        }
    }
}