plugins {

    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.beryx.runtime' version '1.13.0'

}

repositories {

    mavenCentral()

}

javafx {

    version '17'
    modules = [ 'javafx.controls', 'javafx.swing', 'javafx.graphics' ]

}

dependencies {

    implementation 'org.apache.xmlgraphics:batik-all:1.16'

}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
        resources {
            srcDirs 'resources'
        }
    }
}

application {

    mainClass = 'gui.Chess'
    applicationName = 'Chess'
    
}

runtime {

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        noConsole = true
    }

    jpackage {
        mainClass = 'gui.Chess'
        applicationName = 'Chess'
        imageName = 'Chess'
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        imageOptions += ['--icon', "resources/img/icon_256x256.png"]

        installerOptions += ['--resource-dir', "resources"]

        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'Chess','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'Chess']
        }
    }
}